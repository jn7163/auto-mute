/*
 * Copyright (C) 2018 David Sommerich
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

ext {
    supportVersion = '27.0.2'

    // Get version string from git tag
    def describe = ['git', 'describe', '--tags', '--long'].execute().text.trim()
    def m = (describe =~ /v([0-9]+)\.([0-9]+)\.([0-9]+)-([0-9]+)-(.+)/)

    // Parse version tag
    versionMajor = m[0][1].toInteger()
    versionMinor = m[0][2].toInteger()
    versionPatch = m[0][3].toInteger()
    versionRelease = m[0][4].toInteger()
    versionCommit = m[0][5]
}

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "xyz.sommd.automute"
        minSdkVersion 26
        targetSdkVersion 27

        // MAJOR_MINOR_PATCH_RELEASE
        def code = versionMajor * 1_00_00_000 +
                versionMinor * 1_00_000 +
                versionPatch * 1_000 +
                versionRelease
        versionCode code.toInteger()
        versionName "$versionMajor.$versionMinor.$versionPatch"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            versionNameSuffix "-$versionRelease-$versionCommit"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation 'androidx.core:core-ktx:0.1'
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:preference-v14:$supportVersion"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
