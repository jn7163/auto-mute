/*
 * Copyright (C) 2018 David Sommerich
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        // Dependency versions
        sdkVersion = [min: 26, target: 27]
        kotlinVersion = '1.2.30'
        supportVersion = '27.1.1'

        // Get version string from git tag
        def describe = ['git', 'describe', '--tags', '--long'].execute().text.trim()
        def m = (describe =~ /v(\d+)\.(\d+)\.(\d+)-(\d+)-(.+)/)

        // Parse version tag
        def major = m[0][1].toInteger()
        def minor = m[0][2].toInteger()
        def patch = m[0][3].toInteger()
        def release = m[0][4].toInteger()
        def commit = m[0][5]

        // Project version info
        projectVersion = [
                code  : major * 1_00_00_000 +
                        minor * 1_00_000 +
                        patch * 1_000 +
                        release,
                name  : "$major.$minor.$patch",
                suffix: "-$release-$commit"
        ]
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

subprojects {
    apply plugin: 'com.android.application'
    apply plugin: 'kotlin-android'

    repositories {
        google()
        jcenter()
    }

    android {
        compileSdkVersion sdkVersion.target

        defaultConfig {
            minSdkVersion sdkVersion.min
            targetSdkVersion sdkVersion.target

            versionCode projectVersion.code
            versionName projectVersion.name

            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            debug {
                versionNameSuffix projectVersion.suffix
            }

            release {
                minifyEnabled true
                shrinkResources true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        implementation 'androidx.core:core-ktx:0.3'
        implementation "com.android.support:appcompat-v7:$supportVersion"

        testImplementation 'junit:junit:4.12'
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}
